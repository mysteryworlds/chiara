plugins {
  id 'java'
  id 'signing'
  id 'maven-publish'
}

group = 'com.mysteryworlds'
version = '4.0.0'

sourceCompatibility = '16'
targetCompatibility = '16'

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    name 'Jitpack'
    description 'Jitpack'
    url 'https://jitpack.io'
  }
  maven {
    name 'Sonatype Snapshots'
    description 'Sonatype Snapshots'
    url 'https://oss.sonatype.org/content/repositories/snapshots'
  }
  maven {
    name 'Sonatype Central'
    description 'Sonatype Central'
    url 'https://oss.sonatype.org/content/repositories/central'
  }
  maven {
    name 'Spigot'
    description 'SpigotMC.org'
    url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    content {
      includeGroup 'org.bukkit'
      includeGroup 'org.spigotmc'
    }
  }
  maven {
    name 'D3adpsace Theresa GitHub Package Registry'
    description 'D3adpsace Theresa GitHub Package Registry'
    url 'https://maven.pkg.github.com/d3adspace/theresa'
    credentials {
      username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
      password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
    }
  }
}

dependencies {
  implementation 'de.d3adspace:theresa-core:3.4.0'
  compileOnly 'org.bstats:bstats-bukkit:2.2.1'
  compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
  compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
  testImplementation 'org.mockito:mockito-core:3.11.2'
  testImplementation 'org.mockito:mockito-junit-jupiter:3.11.2'
}

test {
  useJUnitPlatform()
}

java {
  withSourcesJar()
  withJavadocJar()
}

jar {
  exclude 'META-INF/LICENSE.txt', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

signing {
  useInMemoryPgpKeys(
    findProperty("GPG_KEY") as String,
    findProperty("GPG_KEY_PASSPHRASE") as String
  )
  sign configurations.archives
}
